PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX base: <http://llama.mshri.on.ca/lore-base.owl#>
PREFIX pheno: <http://llama.mshri.on.ca/lore-phenotype.owl#>
PREFIX genome: <http://llama.mshri.on.ca/lore-genome.owl#>
PREFIX mol: <http://llama.mshri.on.ca/lore-molecules.owl#>
PREFIX ia: <http://llama.mshri.on.ca/lore-interaction.owl#>

SELECT ?allele ?disease ?effect ?iaEntrez
WHERE {
	?gene base:hasXRef ?xref.
	?xref base:hasAuthority <urn:lore:Authority#HGNC>;
		  base:hasValue "TPM3"^^xsd:string .
	?gene genome:hasAllele ?allele.
	OPTIONAL {
		?allele pheno:isAssociatedWith ?disease
	}
	OPTIONAL {
		?allele ?effect ?ia.
		?ia rdf:type ia:PhysicalInteraction;
			ia:hasParticipant/mol:encodedBy ?iaGene.
		FILTER(?iaGene != ?gene)
		?iaGene base:hasXRef ?iaXRef.
		?iaXRef base:hasAuthority <urn:lore:Authority#EntrezGene>;
			base:hasValue ?iaEntrez.
	}
}
