PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX base: <http://llama.mshri.on.ca/lore-base.owl#>
PREFIX pheno: <http://llama.mshri.on.ca/lore-phenotype.owl#>
PREFIX genome: <http://llama.mshri.on.ca/lore-genome.owl#>
PREFIX mol: <http://llama.mshri.on.ca/lore-molecules.owl#>
PREFIX ia: <http://llama.mshri.on.ca/lore-interaction.owl#>

SELECT ?entrezA ?entrezB (COUNT(DISTINCT ?posAllele) AS ?numMaintain) (COUNT(DISTINCT ?negAllele) AS ?numInterrupt)
WHERE {

	?ia ia:hasParticipant ?proteinA;
		ia:hasParticipant ?proteinB.
	FILTER(?proteinA != ?proteinB)

	?proteinA base:hasXRef ?xrefA.
	?xrefA base:hasAuthority <urn:lore:Authority#EntrezGene>;
		base:hasValue ?entrezA.
	?proteinB base:hasXRef ?xrefB.
	?xrefB base:hasAuthority <urn:lore:Authority#EntrezGene>;
		base:hasValue ?entrezB.

	OPTIONAL {
		?proteinA mol:encodedBy/genome:hasAllele ?posAllele.
		?posAllele ia:affectsPositively ?ia.
	}
	OPTIONAL {
		?proteinA mol:encodedBy/genome:hasAllele ?negAllele.
		?negAllele ia:affectsNegatively ?ia.
	}

} 
GROUP BY ?entrezA ?entrezB
ORDER BY ?entrezA ?entrezB